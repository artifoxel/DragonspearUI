BACKUP ~dragonspear_ui++/backup~
AUTHOR ~Pecca (https://forums.beamdog.com/discussion/50357/mod-dragonspear-ui-v2-31/p1)~
VERSION ~22.01.3~

/////////////////////////////////////////////////////////////////////////////////
///  LANGUAGE OPTIONS (WITH ENGLISH FALLBACK)
/////////////////////////////////////////////////////////////////////////////////

LANGUAGE
	~English~ ~dragonspear_ui++/tra/~ ~dragonspear_ui++/tra/english.tra~
LANGUAGE
	~Polski (by Etamin)~ ~dragonspear_ui++/tra/english.tra~ ~dragonspear_ui++/tra/polish.tra~
LANGUAGE
	~Italian (by Aedan)~ ~dragonspear_ui++/tra/english.tra~ ~dragonspear_ui++/tra/italian.tra~
LANGUAGE
	~Russian (by d0lphin)~ ~dragonspear_ui++/tra/english.tra~ ~dragonspear_ui++/tra/russian.tra~
LANGUAGE
	~Brazilian Portuguese (by Felipefpl)~ ~dragonspear_ui++/tra/english.tra~ ~dragonspear_ui++/tra/brazilian portuguese.tra~
LANGUAGE
	~French (by Shodead)~ ~dragonspear_ui++/tra/english.tra~ ~dragonspear_ui++/tra/french.tra~

/////////////////////////////////////////////////////////////////////////////////
///  SOD GUI OVERHAUL CORE COMPONENT
/////////////////////////////////////////////////////////////////////////////////

BEGIN @1000

REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet iwdee~ @1003

ACTION_MATCH 0 WITH
	0 WHEN GAME_IS ~eet~ BEGIN OUTER_SPRINT game "eet" END
	0 WHEN GAME_IS ~bgee~ BEGIN
		ACTION_IF GAME_INCLUDES ~sod~ BEGIN
			OUTER_SPRINT game "sod"
		END ELSE BEGIN
			OUTER_SPRINT game "bg1"
		END
	END
	0 WHEN GAME_IS ~bg2ee~ BEGIN OUTER_SPRINT game "bg2" END
	0 WHEN GAME_IS ~iwdee~ BEGIN OUTER_SPRINT game "iwd" END
	DEFAULT FAIL "unhandled match case"
END

OUTER_SPRINT build_folder "%MOD_FOLDER%/build"
MKDIR "%build_folder%"

ACTION_IF "%WEIDU_OS%" STRING_EQUAL "win32" BEGIN
	AT_NOW exit_code "%MOD_FOLDER%/bin/build.bat %WEIDU_ARCH% %game% 2> %build_folder%/error.log"
END ELSE BEGIN
	AT_NOW exit_code "%MOD_FOLDER%/bin/build.sh %game% 2> %build_folder%/error.log"
END

ACTION_MATCH exit_code WITH
	0 BEGIN /* all good */ END
	42 BEGIN
		WARN "This mod requires lua5.2+ installed."

		ACTION_IF ~%WEIDU_OS%~ STRING_EQUAL "osx" BEGIN
			WARN "You can install it using homebrew or macports:%LNL%"
			   ^ "https://brew.sh%LNL%"
			   ^ "https://www.macports.org%LNL%"
		END ELSE BEGIN
			WARN "You can install it using your OS package manager."
		END

		FAIL "Missing or unsupported Lua version"
	END
	DEFAULT
		FAIL "An error happened file generating UI.menu file"
END


COPY "%build_folder%/ui.menu" ~override~
COPY ~dragonspear_ui++/copy/all~ ~override~

ACTION_IF GAME_IS ~bg2ee eet iwdee~ OR NOT GAME_INCLUDES ~sod~ BEGIN
	COPY ~dragonspear_ui++/copy/eet-bg2~ ~override~
END

ACTION_IF GAME_IS ~bgee~ THEN BEGIN
	COPY ~dragonspear_ui++/copy/eet-sod~ ~override~
	COPY ~dragonspear_ui++/copy/sod~ ~override~
END

ACTION_IF GAME_IS ~bg2ee~ THEN BEGIN
	COPY ~dragonspear_ui++/copy/bg2~ ~override~
END

ACTION_IF GAME_IS ~eet~ THEN BEGIN
	COPY ~dragonspear_ui++/copy/eet-sod~ ~override~
	COPY ~dragonspear_ui++/copy/eet~ ~override~

	COPY ~dragonspear_ui++/copy/sod/rgdback1.wbm~ ~override/rgdback1.wbm~
	COPY ~dragonspear_ui++/copy/sod/rgdback1.wbm~ ~override/rgdback7.wbm~
	COPY ~dragonspear_ui++/copy/sod/rgdback2.wbm~ ~override/rgdback2.wbm~
	COPY ~dragonspear_ui++/copy/sod/rgdback3.wbm~ ~override/rgdback5.wbm~
	COPY ~dragonspear_ui++/copy/bg2/rgdback1.wbm~ ~override/rgdback3.wbm~
	COPY ~dragonspear_ui++/copy/bg2/rgdback2.wbm~ ~override/rgdback4.wbm~
	COPY ~dragonspear_ui++/copy/bg2/rgdback3.wbm~ ~override/rgdback6.wbm~
END

ACTION_IF GAME_IS ~iwdee~ THEN BEGIN
	COPY ~dragonspear_ui++/copy/iwd~ ~override~
END


/////////////////////////////////////////////////////////////////////////////////
///  TRANSPARENT TOOLTIP BACKGROUND
/////////////////////////////////////////////////////////////////////////////////

BEGIN @1007

REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet iwdee~ @1003

COPY ~dragonspear_ui++/copy/tooltip~ ~override~


/////////////////////////////////////////////////////////////////////////////////
///  UPDATE DIALOG.TLK STRINGS
/////////////////////////////////////////////////////////////////////////////////

BEGIN @1001

REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet iwdee~ @1003

STRING_SET ~23084~ @101

STRING_SET ~11947~ @106
STRING_SET ~11949~ @107
STRING_SET ~11951~ @108
STRING_SET ~11952~ @109
STRING_SET ~11953~ @110

ACTION_IF GAME_IS ~bgee~ THEN BEGIN
STRING_SET ~24366~ @102
END
ACTION_IF GAME_IS ~bg2ee eet iwdee~ THEN BEGIN
STRING_SET ~5684~ @102
END


/////////////////////////////////////////////////////////////////////////////////
///  QUICKSAVE SLOTS NUMBER
/////////////////////////////////////////////////////////////////////////////////

BEGIN ~1~

REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet iwdee~ @1003

SUBCOMPONENT @1008

COPY_EXISTING ~savename.2da~ ~override~
	SET_2DA_ENTRY 1 3 4 1


BEGIN ~2~

REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet iwdee~ @1003

SUBCOMPONENT @1008

COPY_EXISTING ~savename.2da~ ~override~
	SET_2DA_ENTRY 1 3 4 2


BEGIN ~3~

REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet iwdee~ @1003

SUBCOMPONENT @1008

COPY_EXISTING ~savename.2da~ ~override~
	SET_2DA_ENTRY 1 3 4 3


BEGIN ~4~

REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet iwdee~ @1003

SUBCOMPONENT @1008

COPY_EXISTING ~savename.2da~ ~override~
	SET_2DA_ENTRY 1 3 4 4


BEGIN ~5~

REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet iwdee~ @1003

SUBCOMPONENT @1008

COPY_EXISTING ~savename.2da~ ~override~
	SET_2DA_ENTRY 1 3 4 5


BEGIN ~6~

REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet iwdee~ @1003

SUBCOMPONENT @1008

COPY_EXISTING ~savename.2da~ ~override~
	SET_2DA_ENTRY 1 3 4 6


/////////////////////////////////////////////////////////////////////////////////
///  BLURRY PORTRAITS FIX
/////////////////////////////////////////////////////////////////////////////////

BEGIN @1009

REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet~ @1003

ACTION_DEFINE_ARRAY blurry_portraits BEGIN schael coran hephern END
ACTION_PHP_EACH blurry_portraits AS _ => v BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~%v%l.bmp~) BEGIN
		COPY_EXISTING ~%v%l.bmp~ ~override/%v%s.bmp~
		COPY_EXISTING ~%v%l.bmp~ ~override/%v%m.bmp~
	END
END

/////////////////////////////////////////////////////////////////////////////////
///  ORIGINAL ICEWIND DALE CURSORS
/////////////////////////////////////////////////////////////////////////////////

BEGIN @1010

REQUIRE_PREDICATE GAME_IS ~iwdee~ @1003
COPY ~dragonspear_ui++/copy/CURSORS.BAM~ ~override~

/////////////////////////////////////////////////////////////////////////////////
///  MUTE TOOLTIP SOUND EFFECT
/////////////////////////////////////////////////////////////////////////////////

BEGIN @1011

REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet iwdee~ @1003
COPY ~G:\_gog\Icewind Dale Enhanced Edition\dragonspear_ui++\copy\paper.wav~ ~override~
