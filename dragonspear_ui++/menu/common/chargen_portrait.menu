menu
{
	name 'CHARGEN_PORTRAIT'
	align center center
	ignoreesc
	onopen "
		MultiPortraitPicker = Infinity_GetINIValue('Game Options','MultiPortraitPicker',1)
		if MultiPortraitPicker == 1 then
			IncPortraitArray()
		else
			duiPPNameFilter1 = ''
			duiPPNameFilter2 = ''
			listPP = ListPortraitPicker:create()
		end
	"
	onclose "
		duiPPNameFilter1 = nil
		duiPPNameFilter2 = nil
		listPP = nil
	"

	label
	{
		enabled "MultiPortraitPicker == 1"
		area 0 0 1444 818
		mosaic 'RGCNAME'
	}
	label
	{
		area 474 84 500 42
		text "APPEARANCE_TITLE"
		text style "title"
	}

	button
	{
		enabled "MultiPortraitPicker == 1"
		area 336 362 52 132
		bam GUIPRC
		sequence 0
		action "DecPortraitArray()"
	}
	button
	{
		enabled "MultiPortraitPicker == 1"
		area 1059 362 52 132
		bam GUIPRC
		sequence 1
		action "IncPortraitArray()"
	}

#local i = 0
#for y = 156, 574, 139 do for x = 400, 965, 94 do i = i + 1
	label { enabled "MultiPortraitPicker == 1" area `x` `y` 80 125 bitmap lua "GetPortrait(`i`)" }
#end end

#local i = 0
#for y = 151, 569, 139 do for x = 395, 960, 94 do i = i + 1
	button { enabled "MultiPortraitPicker == 1" area `x` `y` 90 135 bam "rgcfram" toggle togglePort`i` action "togglePortrait(`i`)" action "OnPortraitArrayClick(`i`)" }
#end end

	label
	{
		enabled "IsPortraitChosen() and MultiPortraitPicker == 1"
		area 1142 248 210 330
		bitmap lua "GetPortrait(portraitChoice)"
	}
	label
	{
		enabled "IsPortraitChosen() and MultiPortraitPicker == 1"
		area 1134 604 230 44
		text lua "GetPortrait(portraitChoice)"
		text style "button"
	}
	button
	{
		on escape
		enabled "MultiPortraitPicker == 1"
		area 484 724 230 44
		bam GUIBUTMT
		text "BACK_BUTTON"
		text style "button"
		action "Infinity_PopMenu(); createCharScreen:OnCancelButtonClick()"
	}
	button
	{
		on return
		enabled "MultiPortraitPicker == 1"
		area 730 724 230 44
		bam GUIBUTMT
		text "DONE_BUTTON"
		text style "button"
		clickable lua "IsPortraitChosen()"
		action "ChoosePortrait(); Infinity_PopMenu(); createCharScreen:OnDoneButtonClick()"
	}

	---- LIST PORTRAIT PICKER ----

	label
	{
		enabled "MultiPortraitPicker == 0"
		area 0 0 1444 818
		mosaic 'RGCGAPP'
	}
--	label
--	{
--		area 82 90 178 44
--		text lua "Infinity_FetchString(32297) .. ': ' .. t(togS)"
--		text style "label"
--		align left center
--	}
	label
	{
		enabled "MultiPortraitPicker == 0"
		area 356 177 346 85
		text 'RG_PP_TOOLTIP'
		text style "label"
		align left center
	}
	label
	{
		enabled "MultiPortraitPicker == 0"
		area 356 498 346 82
		text lua "listPP.portraitCountLabel"
		text style "label"
		align left center
	}
	button
	{
		enabled "MultiPortraitPicker == 0"
		area 0 0 0 0
		bam GUIPRC
		sequence 0
		action "createCharScreen:DecCurrentPortrait()"
	}
	button
	{
		enabled "MultiPortraitPicker == 0"
		area 0 0 0 0
		bam GUIPRC
		sequence 1
		action "createCharScreen:IncCurrentPortrait()"
	}
	label
	{
		enabled "MultiPortraitPicker == 0"
		area 1142 248 210 330
		bitmap lua "listPP.selected and listPP.selected.filename or 'NOPORTMD'"
	}
	button
	{
--		enabled "listPP.selected == nil"
		enabled "MultiPortraitPicker == 0 and not e:IsTouchUI()"
		area 1134 604 230 40
		bam GUIBUTMT
		text "CUSTOM_BUTTON"
		text style "button"
		action "
--			TEXT_popup_big = 17188
			Infinity_PushMenu('RG_PP_POPUP')
			"
	}
	label
	{
--		area 1120 618 234 78
		enabled "MultiPortraitPicker == 0 and listPP.selected ~= nil"
		rectangle 4
	}
	label
	{
		area 356 372 346 60
		enabled "MultiPortraitPicker == 0 and listPP.selected ~= nil"
		text lua "listPP.selected.description"
		text style "label"
		align left center
	}
	label
	{
		enabled "MultiPortraitPicker == 0 and (listPP.selected and listPP.selected.medium) ~= nil"
		area 1206 108 76 108
		rectangle 0
	}
	label
	{
		enabled "MultiPortraitPicker == 0 and (listPP.selected and listPP.selected.medium) ~= nil"
		area 1217 120 54 85
		bitmap lua "listPP.selected.medium"
	}
	label
	{
		enabled "MultiPortraitPicker == 0"
		area 356 446 346 36
		text lua "t(PPStrings.PP_SORT) .. t(listPP.sort.selected.label)"
		text style "label"
		align left center
	}
	button
	{
		on escape
		enabled "MultiPortraitPicker == 0"
		area 420 726 230 40
		bam GUIBUTMT
		text "BACK_BUTTON"
		text style "button"
		action "
			Infinity_PopMenu()
			createCharScreen:OnCancelButtonClick()
		"
	}
	button
	{
		on return
		enabled "MultiPortraitPicker == 0"
		area 806 726 230 40
		bam GUIBUTMT
		text "DONE_BUTTON"
		text style "button"
		clickable lua "listPP.selected ~= nil and createCharScreen:IsDoneButtonClickable()"
		action "
			local filename = listPP.selected.filename
			while createCharScreen:GetCurrentPortrait() ~= filename do
				createCharScreen:IncCurrentPortrait()
			end
			Infinity_PopMenu();
			createCharScreen:OnDoneButtonClick()
		"
	}
#for k, dy in ipairs({ 0, -40 }) do
	edit
	{
		area	392 `668 + dy` 274 32
		enabled "MultiPortraitPicker == 0 and not listPP.invertNameFilter[`k`]"
		var "duiPPNameFilter`k`"
		placeholder "..."
		text style "edit"
		text color 'D'
		maxlines 1
		align left center
		glow lua "duiPPNameFilter`k` ~= ''"
		fill 112 111 111 64
	}
	edit
	{
		area	392 `668 + dy` 274 32
		enabled "MultiPortraitPicker == 0 and listPP.invertNameFilter[`k`]"
		var "duiPPNameFilter`k`"
		placeholder "..."
		text style "edit"
		text color 180 0 0 255
		maxlines 1
		align left center
		fill 112 111 111 64
	}
	button
	{
		enabled "MultiPortraitPicker == 0 and duiPPNameFilter`k` ~= ''"
		area 674 `664 + dy` 34 40
		text "X"
		text style "button"
		action
		"
			duiPPNameFilter`k` = ''
			listPP.invertNameFilter[`k`] = false
		"
	}
	button
	{
		enabled "MultiPortraitPicker == 0"
		area 352 `668 + dy` 36 32
		bam ROUNDBUT
		glow lua "not listPP.invertNameFilter[`k`]"
		action
		"
			local invert = listPP.invertNameFilter
			invert[`k`] = not invert[`k`]
		"
	}
#end
	list
	{
		column
		{
			width	350
			label
			{
				enabled "listPP:filter(rowNumber)"
				area		0 0 329 90
				mosaic lua 	'listPP:portraitBackground(rowNumber, selectedRow)'
				respectClipping
				align		center center
			}
			label
			{
				enabled "listPP:filter(rowNumber)"
				area		0 0 255 90
				text lua	"listPP.portraits[rowNumber].name:upper()"
				text style	"label"
				align		right center
			}
			button
			{
				enabled "listPP:filter(rowNumber)"
				area		270 2 54 85
				bitmap lua 	'listPP.portraits[rowNumber].filename'
				respectClipping
				align		center center
			}
		}
		enabled "MultiPortraitPicker == 0"
		name		'Portrait_Picker'
		area		742 178 350 522
		rowheight	dynamic
		hideHighlight
		table		"listPP.portraits"
		var			"selectedRow"
		scrollbar	'GUISCRC'
		action
		"
			local selected = listPP.portraits[selectedRow]
			if listPP.selected == selected then
				listPP.selected = nil
			else
				listPP.selected = selected
			end
		"
	}
	button
	{
		enabled "MultiPortraitPicker == 0"
		area 1010 146 68 35
--		text lua "PPStrings.PP_SORT_LABEL"
--		text style "button"
		bam RGCSORT
		action "listPP:onSortButtonClicked()"
	}


}